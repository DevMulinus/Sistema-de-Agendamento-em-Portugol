algoritmo "Compra_De_Poltronas_Spazio_Italia_Grupo 1"
var
   assentos: vetor[1..700] de caractere
   cadastro: vetor[1..700, 1..3] de caractere // suporta até 700 usuários, e 3 informações: nome, CPF e e-mail
   nome_evento, horario_evento, dia_evento: caractere
   preco_ingresso: real
   opcao: inteiro
   num_assento: inteiro
   nome, cpf, email: caractere
   i: inteiro

inicio

   para i de 1 ate 700 faca  // diz que i vai de 1 a 700
      assentos[i] <- "Disponivel" // determina que assentos marcados com i serão preenchidos como disponiveis
   fimpara

   // Configurar o evento
   escreval("----- CONFIGURE O EVENTO -----")
   escreva("Informe o nome do evento: ")
   leia(nome_evento)
   escreva("Informe o horário do evento (Ex: 20:00): ")
   leia(horario_evento)
   escreva("Informe o dia da semana do evento: ")
   leia(dia_evento)
   escreva("Informe o preço do ingresso (R$): ")
   leia(preco_ingresso)
   escreval("Evento configurado com sucesso!")
   escreval("Evento: ", nome_evento, ", Dia: ", dia_evento, ", Horário: ", horario_evento, ", Preço: R$", preco_ingresso:0:2)

   // Realizar o cadastro do usuário
   escreval ("")
   escreval("----- CADASTRO DE USUÁRIO -----")
   escreva("Informe seu nome: ")
   leia(nome)
   escreva("Informe seu CPF: ")
   leia(cpf)
   escreva("Informe seu e-mail: ")
   leia(email)

   // Armazenando as informações no cadastro
   cadastro[1, 1] <- nome
   cadastro[1, 2] <- cpf
   cadastro[1, 3] <- email

   escreval("Cadastro realizado com sucesso!")

   // Menu de opções
   repita
      escreval("")
      escreval("----- MENU DE OPÇÕES -----")
      escreval("1. Reservar Assento")
      escreval("2. Cancelar Reserva")
      escreval("3. Mostrar Todos os Assentos")
      escreval("4. Mostrar Informações do Evento")
      escreval("5. Sair")
      escreva("Escolha uma opção: ")
      leia(opcao)

      escolha opcao
      caso 1
         escreva("Digite o número do assento que deseja reservar (1 a 700): ")
         leia(num_assento)
         se (num_assento >= 1) e (num_assento <= 700) entao
            se assentos[num_assento] = "Disponivel" entao
               assentos[num_assento] <- "Reservado"
               escreval("Assento ", num_assento, " reservado com sucesso por R$", preco_ingresso:0:2, ".")
            senao
               escreval("Assento já está reservado.")
            fimse
         senao
            escreval("Número de assento inválido.")
         fimse

      caso 2
         escreva("Digite o número do assento que deseja cancelar a reserva (1 a 700): ")
         leia(num_assento)
         se (num_assento >= 1) e (num_assento <= 700) entao
            se assentos[num_assento] = "Reservado" entao
               assentos[num_assento] <- "Disponivel"
               escreval("Reserva do assento ", num_assento, " cancelada.")
            senao
               escreval("Assento não está reservado.")
            fimse
         senao
            escreval("Número de assento inválido.")
         fimse

      caso 3
         para num_assento de 1 ate 700 faca
            escreva("Assento ", num_assento, ": ")
            se assentos[num_assento] = "Disponivel" entao
               escreval("Disponível")
            senao
               escreval("Reservado")
            fimse
         fimpara

      caso 4
         escreval("") // espaço em branco para fins estéticos
         escreval("----- INFORMAÇÕES DO EVENTO -----")
         escreval("Nome do Evento: ", nome_evento)
         escreval("Horário: ", horario_evento)
         escreval("Dia da Semana: ", dia_evento)
         escreval("Preço do Ingresso: R$", preco_ingresso:0:2)

      caso 5
         escreval("Saindo do sistema.")

      outrocaso
         escreval("Opção inválida.")
      fimescolha
   ate opcao = 5
fimalgoritmo
